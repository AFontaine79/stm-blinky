# Find the final output filename of a target.
# target - A target created by add_executable().
# output_var - OUT - Name of binary file generated by the linker stage.
function(get_executable_name target output_var)
	# First see if this target has set the SUFFIX property directly.
	get_property(exe_suffix TARGET ${target} PROPERTY SUFFIX)
	if(NOT "${exe_suffix}" STREQUAL "")
		# If so, use the SUFFIX property
		set(${output_var} "${target}${exe_suffix}" PARENT_SCOPE)
	# Second, see if a suffix is being applied globally to all executables.
	elseif(NOT "${CMAKE_EXECUTABLE_SUFFIX}" STREQUAL "")
		# If so, use the that.
		set(${output_var} "${target}${CMAKE_EXECUTABLE_SUFFIX}" PARENT_SCOPE)
	else()
		# Otherwise, just use the target name as is.
		set(${output_var} "${target}" PARENT_SCOPE)
	endif()
endfunction()

function(convert_to_hex target)
	get_executable_name(${target} target_exe_name)
	add_custom_target("${target}_hex" ALL
		COMMAND ${CMAKE_OBJCOPY} -O ihex ${target_exe_name} ${target}.hex
		BYPRODUCTS ${target}.hex
	)
	add_dependencies("${target}_hex" ${target})
endfunction()

function(convert_to_bin target)
	get_executable_name(${target} target_exe_name)
	add_custom_target("${target}_bin" ALL
		COMMAND ${CMAKE_OBJCOPY} -O binary ${target_exe_name} ${target}.bin
		BYPRODUCTS ${target}.bin
	)
	add_dependencies("${target}_bin" ${target})
endfunction()
